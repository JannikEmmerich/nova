if (project != rootProject) {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group as String
                artifactId = project.name as String
                version = project.version as String

                pom {
                    name = project.name
                    url = 'https://github.com/getnova/nova-backend'

                    licenses {
                        license {
                            name = "GNU AFFERO GENERAL PUBLIC LICENSE, Version 3"
                            url = "https://raw.githubusercontent.com/getnova/nova-backend/master/LICENSE"
                        }
                    }
                }

                from components.java
            }
        }
    }

    bintray {
        user System.getenv('BINTRAY_USERNAME')
        key System.getenv('BINTRAY_KEY')

        publications = ['maven']

        publish true
        override true

        pkg {
            userOrg = 'getnova'
            repo = 'nova-backend'
            name = project.name
            websiteUrl = 'https://github.com/getnova/nova-backend'
            issueTrackerUrl = 'https://github.com/getnova/nova-backend/issues'
            vcsUrl = 'https://github.com/getnova/nova-backend.git'
            licenses = ['AGPL-V3']
            publicDownloadNumbers = true

            githubRepo = 'getnova/nova-backend'
            githubReleaseNotesFile = 'README.md'

            version {
                name = project.version as String
                released = new Date()
            }
        }
    }
}
