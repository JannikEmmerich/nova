plugins {
    id 'io.freefair.lombok' version '5.1.0' apply false
    id 'io.freefair.git-version' version '5.1.0'
    id 'io.freefair.github.package-registry-maven-publish' version '5.1.0' apply false
    id 'io.freefair.aggregate-javadoc' version '5.1.0'
}

allprojects {
    group 'net.nova-project.backend'
}

version = version.toString().startsWith('v') ? version.toString().substring(1) : version;

subprojects {
    apply plugin: 'java'
    if (project.name != 'nova-bootstrap') apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.freefair.github.package-registry-maven-publish'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14

    dependencies {
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    github {
        slug = 'nova-project/nova-backend'
        username = findProperty('github.username') ?: System.getenv('GITHUB_PACKAGES_USERNAME')
        token = findProperty('github.token') ?: System.getenv('GITHUB_PACKAGES_TOKEN')
        travis = false
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    lombok {
        config.put('lombok.equalsandhashcode.callsuper', 'CALL')
    }
}
