plugins {
    id 'base'
    id 'io.freefair.git-version' version '5.1.0'
    id 'io.freefair.aggregate-javadoc' version '5.1.0'
    id 'io.freefair.lombok' version '5.1.0' apply false
    id 'io.freefair.javadoc-utf-8' version '5.1.0'
//    id 'io.freefair.javadoc-links' version '5.1.0'
    id 'io.freefair.aggregate-jacoco-report' version '5.1.0'
}

repositories {
    jcenter()
}

version version.toString().startsWith('v') ? version.toString().substring(1) : version
version version.toString() == 'master-SNAPSHOT' ? 'edge' : version

allprojects {
    group 'net.getnova.backend'
    version rootProject.version
}

subprojects {
    apply from: "${rootDir}/gradle/java.gradle"
    apply from: "${rootDir}/gradle/lombok.gradle"
    apply from: "${rootDir}/gradle/test.gradle"
    apply from: "${rootDir}/gradle/checkstyle.gradle"
    apply from: "${rootDir}/gradle/javadoc.gradle"
    apply from: "${rootDir}/gradle/jacoco.gradle"
    apply from: "${rootDir}/gradle/maven-publish.gradle"
}

task copyJavaDocStyles(type: Copy) {
    from "$rootDir/gradle/javadoc.css"
    rename "javadoc.css", "stylesheet.css"
    into "$buildDir/docs/aggregateJavadoc/"
}

aggregateJavadoc {
    finalizedBy copyJavaDocStyles
}
